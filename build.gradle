plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
    id "com.github.ben-manes.versions" version "0.42.0"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.github.gmazzo.buildconfig' version '3.1.0'
}

repositories {
    //maven { url = 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/kotlin-js-wrappers' }
    google()
    mavenCentral()
    maven { url = 'https://jitpack.io' }
}

def mainName = 'ContentServer'
def appName = 'server'

// This is for ./gradlew run
//application {
//    mainClassName = mainName
//}

sourceCompatibility = 1.8
targetCompatibility = 1.8

description 'ReadingBat Site'
group 'com.github.readingbat'
version '1.10.3'

buildConfig {
    buildConfigField('String', 'SITE_NAME', "\"${project.name}\"")
    buildConfigField('String', 'SITE_VERSION', "\"${project.version}\"")
    buildConfigField('String', 'SITE_RELEASE_DATE', "\"07/16/2022\"")
}

dependencies {
    implementation "com.github.readingbat.readingbat-core:readingbat-core:$readingbat_version"
    implementation "com.github.pambrose.common-utils:core-utils:$utils_version"
    implementation "io.github.microutils:kotlin-logging:$logging_version"
}

task stage(dependsOn: ['uberjar', 'build', 'clean'])
build.mustRunAfter clean

shadowJar {
    zip64 true
    mergeServiceFiles()
    exclude "META-INF/*.SF"
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude "LICENSE*"
}

task uberjar(type: Jar, dependsOn: shadowJar) {
    zip64 true
    archiveFileName = 'server.jar'
    manifest {
        attributes('Implementation-Title': appName)
        attributes('Implementation-Version': version)
        attributes('Built-Date': new Date())
        attributes('Built-JDK': System.getProperty('java.version'))
        attributes('Main-Class': mainName)
    }
    from zipTree(shadowJar.archiveFile)
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ['-Xbackend-threads=8']
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ['-Xbackend-threads=8']
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showStandardStreams = true
    }
}